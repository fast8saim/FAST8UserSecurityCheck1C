
&НаСервере
Процедура ПроверитьПользователейНаСервере()
	
	ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ПользовательИБ Из ПользователиИБ Цикл
		
		Небезопасно = Ложь;
		ПолныеПрава							= Ложь;
		ИнтерактивноеОткрытиеВО				= Ложь;
		ПравоАдминистрированияПользователей	= Ложь;
		ОтключенаЗащитаОпасныхДействий		= Ложь;
		ОтсутствуетПароль					= Ложь;
		РедактированиеСправочникаДополнительныхОбработок	= Ложь;
		РедактированиеСправочниковПрофилиГруппыДоступа		= Ложь;
		
		Если ПользовательИБ.Роли.Содержит(Метаданные.Роли.ПолныеПрава) Тогда
			ПолныеПрава = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		Если ПравоДоступа("ИнтерактивноеОткрытиеВнешнихОбработок", Метаданные, ПользовательИБ) Или ПравоДоступа("ИнтерактивноеОткрытиеВнешнихОтчетов", Метаданные, ПользовательИБ) Тогда
			ИнтерактивноеОткрытиеВО = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		Если ПравоДоступа("Администрирование", Метаданные, ПользовательИБ) Тогда
			ПравоАдминистрированияПользователей = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		Если ПользовательИБ.ЗащитаОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях	= Ложь Тогда
			ОтключенаЗащитаОпасныхДействий = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		Если ПользовательИБ.АутентификацияСтандартная = Истина И ПользовательИБ.ПарольУстановлен = Ложь Тогда
			ОтсутствуетПароль = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		Если ПравоДоступа("Изменение", Метаданные.Справочники.ДополнительныеОтчетыИОбработки, ПользовательИБ) Тогда
			РедактированиеСправочникаДополнительныхОбработок = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		Если ПравоДоступа("Изменение", Метаданные.Справочники.ПрофилиГруппДоступа, ПользовательИБ) Или ПравоДоступа("Изменение", Метаданные.Справочники.ГруппыДоступа, ПользовательИБ) Тогда
			РедактированиеСправочниковПрофилиГруппыДоступа = Истина;
			Небезопасно = Истина;
		КонецЕсли;
		
		Если Небезопасно = Истина Тогда
			НоваяСтрока = ОпасныеПользователи.Добавить();
			НоваяСтрока.ИмяПользователя						= ПользовательИБ.Имя;
			НоваяСтрока.ПолныеПрава							= ПолныеПрава;
			НоваяСтрока.ИнтерактивноеОткрытиеВО				= ИнтерактивноеОткрытиеВО;
			НоваяСтрока.ПравоАдминистрированияПользователей	= ПравоАдминистрированияПользователей;
			НоваяСтрока.ОтключенаЗащитаОпасныхДействий		= ОтключенаЗащитаОпасныхДействий;
			НоваяСтрока.ОтсутствуетПароль					= ОтсутствуетПароль;
			НоваяСтрока.РедактированиеСправочникаДополнительныхОбработок	= РедактированиеСправочникаДополнительныхОбработок;
			НоваяСтрока.РедактированиеСправочниковПрофилиГруппыДоступа		= РедактированиеСправочниковПрофилиГруппыДоступа;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПользователей(Команда)
	ПроверитьПользователейНаСервере();
КонецПроцедуры
